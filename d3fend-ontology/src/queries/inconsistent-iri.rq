PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl:  <http://www.w3.org/2002/07/owl#>
PREFIX d3f:  <http://d3fend.mitre.org/ontologies/d3fend.owl#>

SELECT DISTINCT ?entity ?property ?packed_label
WHERE {
  VALUES ?top_class { d3f:D3FENDCore d3f:D3FENDKBThing }
  ?entity rdfs:subClassOf* ?top_class .

  MINUS {
    { ?entity rdfs:subClassOf* d3f:OffensiveTactic }
    UNION
    { ?entity rdfs:subClassOf* d3f:Weakness }
    UNION
    { ?entity rdfs:subClassOf* d3f:OSAPIFunction }
  }

  BIND(rdfs:label AS ?property)
  ?entity ?property ?value .

  # Strip punctuation not allowed in IRIs; normalize whitespace
  BIND(STR(?value) AS ?raw)
  BIND(REPLACE(?raw, "[â€™']", "") AS ?p0)                  # apostrophes
  BIND(REPLACE(?p0, "/", "") AS ?p1)                      # slashes
  BIND(REPLACE(?p1, "_", "") AS ?p2)                      # underscores
  #BIND(REPLACE(?p2, "-", "") AS ?p3)                      # hyphens
  BIND(REPLACE(REPLACE(?p2, "\\(", ""), "\\)", "") AS ?p4)# parentheses
  BIND(REPLACE(?p4, "\\s+", " ") AS ?ws1)                 # collapse spaces
  BIND(REPLACE(?ws1, "^\\s+", "") AS ?ws2)                # trim left
  BIND(REPLACE(?ws2, "\\s+$", "") AS ?norm0)              # trim right

  # Title-case only short prepositions (<= 4 chars)
  BIND(REPLACE(REPLACE(REPLACE(?norm0, " of ", " Of "), "^of ", "Of "), " of$", " Of") AS ?c1)
  BIND(REPLACE(REPLACE(REPLACE(?c1, " to ", " To "), "^to ", "To "), " to$", " To") AS ?c2)
  BIND(REPLACE(REPLACE(REPLACE(?c2, " from ", " From "), "^from ", "From "), " from$", " From") AS ?c3)
  BIND(REPLACE(REPLACE(REPLACE(?c3, " with ", " With "), "^with ", "With "), " with$", " With") AS ?c4)
  BIND(REPLACE(REPLACE(REPLACE(?c4, " over ", " Over "), "^over ", "Over "), " over$", " Over") AS ?c5)
  BIND(REPLACE(REPLACE(REPLACE(?c5, " into ", " Into "), "^into ", "Into "), " into$", " Into") AS ?c6)
  BIND(REPLACE(REPLACE(REPLACE(?c6, " onto ", " Onto "), "^onto ", "Onto "), " onto$", " Onto") AS ?c7)
  BIND(REPLACE(REPLACE(REPLACE(?c7, " upon ", " Upon "), "^upon ", "Upon "), " upon$", " Upon") AS ?c8)
  BIND(REPLACE(REPLACE(REPLACE(?c8, " down ", " Down "), "^down ", "Down "), " down$", " Down") AS ?c9)
  BIND(REPLACE(REPLACE(REPLACE(?c9, " in ", " In "), "^in ", "In "), " in$", " In") AS ?c10)
  BIND(REPLACE(REPLACE(REPLACE(?c10, " on ", " On "), "^on ", "On "), " on$", " On") AS ?c11)
  BIND(REPLACE(REPLACE(REPLACE(?c11, " at ", " At "), "^at ", "At "), " at$", " At") AS ?c12)
  BIND(REPLACE(REPLACE(REPLACE(?c12, " as ", " As "), "^as ", "As "), " as$", " As") AS ?c13)
  BIND(REPLACE(REPLACE(REPLACE(?c13, " via ", " Via "), "^via ", "Via "), " via$", " Via") AS ?c14)
  BIND(REPLACE(REPLACE(REPLACE(?c14, " per ", " Per "), "^per ", "Per "), " per$", " Per") AS ?c15)
  BIND(REPLACE(REPLACE(REPLACE(?c15, " by ", " By "), "^by ", "By "), " by$", " By") AS ?c16)
  BIND(REPLACE(REPLACE(REPLACE(?c16, " for ", " For "), "^for ", "For "), " for$", " For") AS ?c17)
  BIND(REPLACE(REPLACE(REPLACE(?c17, " up ", " Up "), "^up ", "Up "), " up$", " Up") AS ?c18)
  BIND(REPLACE(REPLACE(REPLACE(?c18, " out ", " Out "), "^out ", "Out "), " out$", " Out") AS ?c19)
  BIND(REPLACE(REPLACE(REPLACE(?c19, " off ", " Off "), "^off ", "Off "), " off$", " Off") AS ?c20)
  # Title-case conjunctions 
  BIND(REPLACE(?c20, " and ", " And ") AS ?c21)
  BIND(REPLACE(?c21, " nor ", " Nor ") AS ?c22)
  BIND(REPLACE(?c22, " but ", " But ") AS ?norm)

  # Build the default packed label (remove spaces after capitalization passes)
  BIND(REPLACE(?norm, " ", "") AS ?packed_default)

  # Manual overrides for HTTP events from the violation list (Http* instead of HTTP*)
  OPTIONAL {
    VALUES (?exception_label ?exception_packed) {
      ("HTTP GET Event"     "HTTPGetEvent")
      ("HTTP POST Event"    "HTTPPostEvent")
      ("HTTP PUT Event"     "HTTPPutEvent")
      ("HTTP DELETE Event"  "HTTPDeleteEvent")
      ("HTTP OPTIONS Event" "HTTPOptionsEvent")
      ("HTTP TRACE Event"   "HTTPTraceEvent")
      ("HTTP CONNECT Event" "HTTPConnectEvent")
      ("HTTP HEAD Event"    "HTTPHeadEvent")
    }
    FILTER(?value = ?exception_label)
  }

  BIND(COALESCE(?exception_packed, ?packed_default) AS ?packed_label)

  # If an HTTP exception matched, require exact case; otherwise allow case-insensitive match
  FILTER NOT EXISTS {
    FILTER(regex(STR(?entity), CONCAT(".*#", ?packed_label, "$")))
  }

  # Exclude anything under OffensiveTechnique
  FILTER NOT EXISTS {
    ?tactic_technique rdfs:subClassOf d3f:OffensiveTechnique .
    ?entity rdfs:subClassOf+ ?tactic_technique .
  }
}
